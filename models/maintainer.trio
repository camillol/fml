class Maintainer;
	
	visible:
		startwork, working;

	temporal domain natural;

	TD items
		predicates
			startwork; --this is true when the maintainer starts working on the collector			
			working; -- this is true when the maintainer is working on the collector
	TI items
		consts
			min_maintenance_interval, max_maintenance_interval, worktime: real;	-- year, week and time required to complete the procedure expressed in the system time unit
			
	axioms
		vars
			t: real;
			
		enforce_maximum_interval:
			Lasted(¬working, max_maintenance_interval) -> startwork
		
		enforce_minimum_interval:
			startwork -> AlwP(¬startwork) v ∃ t ( LastTime(startwork, t) ^ t > min_maintenance_interval )
			
		initially_not_working:
			AlwP(¬startwork) -> ¬working;
			
		work_after_start:
			startwork -> Lasts(working, worktime);
			
		stop_working:
			Lasted(working, worktime) -> Until_ie(¬working, startwork);

end Maintainer.
